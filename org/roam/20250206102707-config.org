:PROPERTIES:
:ID:       2ea5dd06-ee06-4ddc-aaf2-f09821bdf532
:HEADER-ARGS: :tangle "~/.config/emacs/init.el"
:END:
#+title: config
#+filetags: :infrastructure:@todo:
* Baseline configuration & setup
** Moving ~custom~ configurations out of ~init.el~
#+begin_src elisp
(setq custom-file "~/.config/emacs/custom.el")
(load custom-file)
#+end_src
** Ignore nativecomp warnings
   #+begin_src elisp
   (setq native-comp-async-report-warnings-errors nil)
   #+end_src
** ~package~ setup
#+begin_src elisp
(require `package)
(setq package-archives '(("melpa" . "https://melpa.org/packages/")))
(package-initialize)
(unless package-archive-contents
  (package-refresh-contents))
#+end_src
** ~use-package~ setup 
#+begin_src elisp
(unless (package-installed-p 'use-package)
  (package-install 'use-package))
(require 'use-package)
(setq use-package-always-ensure t)
#+end_src
** Set ~PATH~ 
We'll wanna grab the ~$PATH~ at startup
#+begin_src elisp
(when (daemonp)
  (exec-path-from-shell-initialize))
#+end_src
** Recent files and ~recentf~
#+begin_src elisp
(recentf-mode 1)
(setq recentf-max-menu-items 10
      recentf-max-saved-items 10)
#+end_src
*** TODO Configure ~recentf-mode~ to allow =evil-mode= keys
** Backup files
#+begin_src elisp
(setq backup-directory-alist `(("~/.saves"))
      backup-by-copying t)

(setq auto-save-file-name-transforms `((".*" "~/.saves/" t)))
#+end_src
*** TODO This doesn't work
** Startup messages
Disable the startup and scratch-buffer messages
#+begin_src elisp
(setq inhibit-startup-message t
      initial-scratch-message "")
#+end_src
** Basic interface & appearance
#+begin_src elisp
(global-hl-line-mode)
(scroll-bar-mode -1)
(tool-bar-mode -1)
(tooltip-mode -1)
(menu-bar-mode -1)
(set-fringe-mode 0)
(setq scroll-conservatively most-positive-fixnum)
(add-hook 'prog-mode-hook 'display-line-numbers-mode)
#+end_src
** Fonts (re-sort later)
#+begin_src elisp
  (setq global-fixed-pitch-font "Inconsolata Nerd Font Mono")
  (setq global-variable-pitch-font "Liberation Sans")

  (when (member global-fixed-pitch-font (font-family-list))
    (set-face-attribute 'default nil :font global-fixed-pitch-font :width 'normal :height 100)
    (set-face-attribute 'fixed-pitch nil :family "Droid Sans Mono"))

  (when (member global-variable-pitch-font (font-family-list))
    (set-face-attribute 'variable-pitch nil :family global-variable-pitch-font :height 1.0))
#+end_src
** Theme (re-sort later)
#+begin_src elisp
(setq custom-theme-load-path '("~/.config/emacs/emacs-theme-gruvbox"))
(load-theme 'gruvbox-dark-medium t)
#+end_src
** Reload ~config.el~ from anywhere
#+begin_src elisp
(defun reload-config ()
  (interactive)
  (load "~/.config/emacs/init.el"))
#+end_src
** Message completion
#+begin_src elisp
(message "Config-status: Completed baseline configuration & setup")
#+end_src
* Treesitter
** Grammars
#+begin_src elisp
(setq treesit-language-source-alist
   '((bash "https://github.com/tree-sitter/tree-sitter-bash")
     (cmake "https://github.com/uyha/tree-sitter-cmake")
     (css "https://github.com/tree-sitter/tree-sitter-css")
     (elisp "https://github.com/Wilfred/tree-sitter-elisp")
     (go "https://github.com/tree-sitter/tree-sitter-go")
     (html "https://github.com/tree-sitter/tree-sitter-html")
     (javascript "https://github.com/tree-sitter/tree-sitter-javascript" "master" "src")
     (json "https://github.com/tree-sitter/tree-sitter-json")
     (make "https://github.com/alemuller/tree-sitter-make")
     (markdown "https://github.com/ikatyang/tree-sitter-markdown")
     (python "https://github.com/tree-sitter/tree-sitter-python")
     (toml "https://github.com/tree-sitter/tree-sitter-toml")
     (tsx "https://github.com/tree-sitter/tree-sitter-typescript" "master" "tsx/src")
     (typescript "https://github.com/tree-sitter/tree-sitter-typescript" "master" "typescript/src")
     (dockerfile "https://github.com/camdencheek/tree-sitter-dockerfile")
     (yaml "https://github.com/ikatyang/tree-sitter-yaml")))
#+end_src
** Installation
#+begin_src elisp
;; (mapc #'treesit-install-language-grammar
;;       (mapcar #'car treesit-language-source-alist))
#+end_src
** Major mode redirection
#+begin_src elisp
(add-to-list 'major-mode-remap-alist '(python-mode . python-ts-mode))
#+end_src
** ~evil-textobj-tree-sitter~
#+begin_src elisp
(use-package evil-textobj-tree-sitter
  :ensure t)
;; bind `function.outer`(entire function block) to `f` for use in things like `vaf`, `yaf`
(define-key evil-outer-text-objects-map "f" (evil-textobj-tree-sitter-get-textobj "function.outer"))
;; bind `function.inner`(function block without name and args) to `f` for use in things like `vif`, `yif`
(define-key evil-inner-text-objects-map "f" (evil-textobj-tree-sitter-get-textobj "function.inner"))

;; You can also bind multiple items and we will match the first one we can find
(define-key evil-outer-text-objects-map "a" (evil-textobj-tree-sitter-get-textobj ("conditional.outer" "loop.outer")))
#+end_src
** Message completion
#+begin_src elisp
(message "Config-status: Completed treesitters")
#+end_src
* Keys
** Maps
*** File IO
This keymap ~file-io-map~ will define various basic file access & management tasks. No packages will add to this one, so we'll just populate all the keys we want and call it good
#+begin_src elisp
(defvar-keymap file-io-map
  :doc "Keybind map for basic file IO tasks"
  "r" #'recentf-open-files
  "f" #'find-file
  "d" #'dired)
#+end_src
*** Buffer things
This keymap ~buffer-menu-map~, like ~file-io-map~, defines a few basic buffer-related tasks. Once again, nothing here that touches packages, so we'll define it and that'll be it
#+begin_src elisp
(defvar-keymap buffer-menu-map
  :doc "Keybind map for buffer tasks"
  "s" #'scratch-buffer
  "m" #'buffer-menu
  "K" #'kill-buffer)
#+end_src
*** ~org-download~
This keymap ~org-download-map~ is solely for ~org-download~ commands. We'll populate it later when we get ~org-download~ going, so for now we'll just make it and leave it empty
#+begin_src elisp
(defvar-keymap org-download-map
  :doc "Keybind for org-download tools")
#+end_src
*** Org
This keymap ~org-io-map~ is for various org related tasks (capture, agenda, etc.). We'll populate with baseline org functions, leaving anything like ~org-roam~ for later
#+begin_src elisp
(defvar-keymap org-io-map
  :doc "Keybind map for org stuff"
  "a" 'org-agenda
  "p" 'org-latex-preview
  "d" `("Org-download options" . ,org-download-map))
#+end_src
*** Global
This keymap ~global-prefix-map~ is the *base* map that all prior keymaps will branch off from. As such, everything here should be treated as either a "root" for some other functionality, or an oft-used shortcut that doesn't flow into anything else. We'll populate it with the previous keymaps, leaving a few other bindings for later with their packages
#+begin_src elisp
(defvar-keymap global-prefix-map
  :doc "Global custom prefix map"
  "o" `("Org IO Keybinds" . ,org-io-map)
  "f" `("File IO Keybinds" . ,file-io-map)
  "b" `("Buffer & Options" . ,buffer-menu-map)
  "r" 'reload-config)
#+end_src
** Binding the maps (and the keys)
This will include more later, but for now we'll just bind the ~global-prefix-map~ to =C-SPC=
#+begin_src elisp
(keymap-set global-map "C-SPC" global-prefix-map)
#+end_src
** Message completion
#+begin_src elisp
(message "Config-status: Completed keys")
#+end_src
* Packages (excl. Org, LSP)
** ~neotree~
#+begin_src elisp
(use-package neotree
  :bind (:map global-prefix-map
	      ("t" . 'neotree-toggle))
  :config
  (setq neo-window-fixed-size nil
	neo-window-width 55)
  (setq neo-theme (if (display-graphic-p) 'nerd-icons 'nerd-icons)))
#+end_src
** ~pdf-tools~
#+begin_src elisp
(use-package pdf-tools)
#+end_src
** ~vmd~
#+begin_src elisp
(use-package vmd-mode
  :hook (markdown-mode . vmd-mode)
  :config
  (setq vmd-binary-path "~/.config/nvm/versions/node/v20.14.0/bin/vmd"))
#+end_src
** ~mood-line~
#+begin_src elisp
(use-package mood-line
  :config
  (mood-line-mode)

  :custom
  (mood-line-glyph-alist mood-line-glyphs-ascii)
  (setq mood-line-format mood-line-format-default-extended))
#+end_src
** ~rainbow-delimiters~
#+begin_src elisp
(use-package rainbow-delimiters
  :config
  (add-hook 'prog-mode-hook 'rainbow-delimiters-mode))
#+end_src
** ~evil~
#+begin_src elisp
(use-package evil
  :init
  (setq evil-want-keybinding t)
  (setq evil-undo-system 'undo-fu)
  :config
  (evil-mode 1))
#+end_src
** =evil-collection=
#+begin_src elisp
(use-package evil-collection
  :after evil
  :config
  (evil-collection-init))
#+end_src
** =evil-org=
#+begin_src elisp
(use-package evil-org
  :ensure t
  :after org
  :hook (org-mode . evil-org-mode)
  :config
  (require 'evil-org-agenda)
  (evil-org-agenda-set-keys))
#+end_src
** ~ivy~
#+begin_src elisp
(use-package ivy
  :diminish
  :bind (("C-s" . swiper)
	 :map ivy-minibuffer-map
	 ("TAB" . ivy-alt-done)
	 ("C-j" . ivy-next-line)
	 ("C-k" . ivy-previous-line))
  :config
  (setq ivy-use-selectable-prompt t))

(ivy-mode 1)
#+end_src
** ~ivy-rich~
#+begin_src elisp
(use-package ivy-rich
  :after ivy)

(require 'ivy-rich)
(ivy-rich-mode 1)
#+end_src
** ~counsel~
#+begin_src elisp
(use-package counsel
  :config
  (counsel-mode 1))
#+end_src
** ~ivy-prescient~
#+begin_src elisp
(use-package ivy-prescient
  :after counsel
  :custom
  (ivy-prescient-enable-filtering nil)
  :config
  (ivy-prescient-mode 1))
#+end_src
** ~company~
#+begin_src elisp
(use-package company
  :bind (:map global-prefix-map
	      ("l" . 'company-complete))
  :config
  (setq company-idle-delay 0)
  (setq company-tooltip-align-annotations t)
  (setq company-tooltip-limit 8)
  (setq company-dabbrev-minimum-length 4)
  (setq company-dabbrev-other-buffers t)
  
  (setq company-backends '((company-capf company-dabbrev company-ispell)))
  (setq company-transformers '(company-sort-by-occurrence company-sort-by-backend-importance))
  (global-company-mode))
#+end_src
** ~which-key~
#+begin_src elisp
(use-package which-key
  :defer 0
  :diminish which-key-mode
  :config
  (which-key-mode)
  (setq which-key-idle-delay 0)) ;; delay before showing key guide 
#+end_src
** ~elfeed~
#+begin_src elisp
(use-package elfeed
  :config
  (setq elfeed-search-title-max-width 120)
  (setq elfeed-feeds
	'(("https://ludic.mataroa.blog/rss/" blog) 
	  ("https://racer.com/indycar/feed/" racing)))

  (defface important-elfeed-entry
    '((t :forefround "#f77"))
    "Import Elfeed entries.")
  (push '(important important-elfeed-entry)
	elfeed-search-face-alist)

  (add-hook 'elfeed-new-entry-hook
	    (elfeed-make-tagger :before "1 month ago" :remove 'unread)))
#+end_src
*** Feeds
**** Ludic
:PROPERTIES:
:URL: https://ludic.mataroa.blog/rss/
:Categories: blog programming
:END:
**** RACER Indycar
:PROPERTIES:
:URL: https://racer.com/indycar/feed/
:Categories: racing
:END:
*** Handlers
Ughhhhhhh quoting the categories wont work
#+begin_src elisp
(defun my-org-elfeed-compile-category ()
  (interactive)
  (progn
    (setq elfeed-feeds ())
    (org-map-entries (lambda ()
			(let*
			    ((categories nil)
			    (url nil))
			(when-let
			    ((url (org-entry-get (point) "URL"))
				(categories (org-entry-get (point) "Categories")))
			  (add-to-list 'elfeed-feeds (append (list url) (split-string categories " "))))))
		    nil 'tree)))
#+end_src
** ~helpful~
** =magit=
#+begin_src elisp
(use-package magit)
#+end_src
** icons
   #+begin_src elisp
   (use-package nerd-icons-completion)
   (use-package nerd-icons-ivy-rich)
   #+end_src
** Message completion
#+begin_src elisp
(message "Config-status: Completed packages")
#+end_src
* LSP 
#+begin_src elisp
(use-package lsp-mode
  :init
  (setq lsp-keymap-prefix "C-c l")
  (yas-global-mode)
  :hook (
	 (python-mode . lsp)
	 (python-ts-mode . lsp)
	 ;; (sh-mode . lsp) ; Requires shellcheck, shfmt ;; Something wrong, freezes client
	 (rust-mode . lsp)
	 (c-mode . lsp)
	 (c++-mode . lsp)
	 (lsp-mode . lsp-enable-which-key-integration)
	 (lsp-mode . lsp-ui-mode))
  :commands lsp
  :config
  (setq lsp-prefer-flymake t)
  (setq lsp-pylsp-plugins-jedi-completion-enabled t)
  (setq lsp-pylsp-plugins-jedi-completion-fuzzy t)
  (setq lsp-eldoc-render-all nil)
  (setq lsp-inlay-hint-enable nil)
  ;; (setq lsp-eldoc-enable-hover t)
  (setq lsp-signature-doc-lines 5)
  (setq lsp-signature-render-documentation nil)
  (setq lsp-signature-auto-activate nil))
#+end_src
** ~lsp-ui~
#+begin_src elisp
(use-package lsp-ui
  :ensure t
  :commands lsp-ui-mode
  :config
  (lsp-ui-peek-enable nil)
  (setq lsp-ui-doc-enable nil)
  (setq lsp-ui-doc-show-with-cursor nil)
  (setq lsp-ui-doc-side 'left)
  (setq lsp-ui-doc-delay 1)
  (setq lsp-ui-doc-position 'at-point))
#+end_src
** ~lsp-ivy~
#+begin_src elisp
(use-package lsp-ivy
  :commands lsp-ivy-workspace-symbol)
#+end_src
** Language specific
#+begin_src elisp
  (use-package docker-compose-mode)
  (use-package dockerfile-mode)
  (use-package mermaid-mode)
  (use-package spice-mode)
  (use-package python-mode)
  (use-package rust-mode)
  (use-package flycheck-rust)
  (use-package json-mode)
  (use-package flycheck)
#+end_src
*** Rustic
#+begin_src elisp
  (use-package rustic
    :ensure
    :config
    (setq rustic-format-on-save t))
#+end_src
** Message completion
#+begin_src elisp
(message "Config-status: Completed LSP")
#+end_src
* Org
** Base behavior
*** Hooks
#+begin_src elisp
(add-to-list 'auto-mode-alist '("\\.org\\'" . org-mode))
(add-hook 'org-mode-hook 'org-indent-mode)
(add-hook 'org-mode-hook 'visual-line-mode)
#+end_src
*** Tags
#+begin_src elisp
(setq org-tag-alist
      '((:startgroup . nil) ;; Subject
	("@mcity" . ?m)
	("@class" . ?c)
	("@avc" . ?a)
	("@waves" .?w)
	("@personal" . ?p)
	(:endgroup . nil)
	(:startgroup . nil) ;; Issue type
	("issue_outstanding" . ?i)
	("issue_fixed" . ?S)
	(:endgroup . nil)
	("emacs_infra" . ?I)))
#+end_src
** Roam
*** ~use-package~ statement
#+begin_src elisp
(use-package org-roam
  :init
  (org-roam-db-autosync-mode)
  :bind (:map org-io-map
	      ("c" . 'org-roam-capture)
	      ("f" . 'org-roam-node-find))
  :config
  (require 'org-roam-export)
  (setq org-roam-node-display-template
	(concat "${title:*} "
		(propertize "${tags:30}" 'face 'org-tag)
		(propertize "${category:20}" 'face 'org-category)))
  (setq org-roam-completion-everywhere t)
  (setq org-roam-directory (file-truename "~/org/roam/")))
#+end_src
*** Capture
#+begin_src elisp
(add-hook 'org-capture-after-finalize-hook 'my-org-exit-frame-if-fleeting)

(setq org-roam-capture-templates
      '(
        ("d" "default node" plain "%?"
         :target (file+head "%<%Y%m%d%H%M%S>-${slug}.org"
                            "#+title: ${title}\n#+filetags: %^g")
         :unnarrowed t)
        ("l" "lecture notes" plain
        "* Lecture Notes\n- %?"
        :target (file+head "lecture-notes/${title}.org"
                    "#+filetags: lecture-%^{prompt||iss308|ece366|mth425|cse331|ece309} \n#+title: ${title}\n")
        :unnarrowed t)
        ("t" "todo" entry
         "* TODO %?"
         :target (node "todos")
         :kill-buffer t)
	("M" "meeting" entry
	 "* Meeting %^{title||WAVES|AVC} %T %^G\n** Notes\n- %?\n** Action items\n*** "
	 :target (node "meeting notes"))
        ))

#+end_src
*** TODOs
#+begin_src elisp
(setq org-log-done 'time)
(setq org-agenda-skip-deadline-if-done t)

(setq org-todo-keywords
      '((sequence
	 "IDEA(i)"
	 "TODO(t)"
	 "INACTIVE(I)"
	 "DOING(d)"
	 "BLOCKED(b@)"
	 "|"
	 "COMPLETE(c!)"
	 "NOT DOING(n!)")))
#+end_src
*** Packages
**** ~org-roam-ql~
#+begin_src elisp
(use-package org-roam-ql
  :after (org-roam)
  :bind ((:map org-roam-mode-map
	       ("v" . org-roam-ql-buffer-dispatch)
	       :map minibuffer-mode-map
	       ("C-c n i" . org-roam-ql-insert-node-title))))
#+end_src
**** ~org-download~
#+begin_src elisp
(use-package org-download
  :bind (:map org-download-map
	      ("c" . 'org-download-clipboard))
  :hook (org-mode . org-download-enable))
#+end_src
*** Custom helper methods
**** Tag retrieval
#+begin_src elisp
(defun my-org-get-tags-at-point-safe ()
  (interactive)
  (or (split-string (or (org-entry-get (point) "TAGS") "nil") ":" t) "nil"))

(defun my-org-get-tags-list (point)
  (split-string (or (org-entry-get (point) "TAGS") "nil") ":" t))
#+end_src
**** Auto-TODO marking
***** Check if buffer has TODO entry
#+begin_src elisp
(defun my-org-has-todo ()
  "Return `t` if buffer has a TODO, `nil` otherwise"
  (interactive)
  (org-element-map
      (org-element-parse-buffer 'headline)
      'headline
    (lambda (h)
      (eq (org-element-property :todo-type h)
	  'todo))
    nil 'first-match))
#+end_src
***** Add tag if it does
#+begin_src elisp
(defun my-org-mark-if-has-todo ()
  "Add tag \"@todo\" to node's filetags if a TODO is present"
  (interactive)
  (if (and (string-equal major-mode "org-mode") (org-roam-file-p))
      (let (tags (org-roam-node-tags (org-roam-node-at-point)))
	(if (and (my-org-has-todo)
		 (not (member "@todo" (org-roam-node-tags (org-roam-node-at-point)))))
	    (org-roam-set-keyword
	     "filetags"
	     (concat
	      ":"
	      (string-join (org-roam-node-tags (org-roam-node-at-point)) ":")
	      (if (org-roam-node-tags (org-roam-node-at-point)) ":")
	      "@todo:"))))))
#+end_src
****** TODO Add capability to remove @todo if TODO is not present
***** Setup hooks
****** ~find-file-hook~
Run when a buffer is loaded from a file
#+begin_src elisp
(add-hook 'find-file-hook #'my-org-mark-if-has-todo)
#+end_src
****** ~before-save-hook~
Run before a buffer is saved
#+begin_src elisp
(add-hook 'before-save-hook #'my-org-mark-if-has-todo)
#+end_src
***** Get all marked
#+begin_src elisp
(defun my-org-all-todo-files ()
  "Use org-roam's DB to query all nodes with @todo marked"
  (interactive)
  (seq-uniq
   (seq-map
    #'car
    (org-roam-db-query
     [:select [nodes:file]
	      :from tags
	      :left-join nodes
	      :on (= tags:node-id nodes:id)
	      :where (like tag (quote "%\"@todo\"%"))]))))
#+end_src
***** Run whenever agenda is run
#+begin_src elisp
(defun my-org-agenda-files-update (&rest _)
  "Update `org-agenda-files` dynamically based on @todo"
  (setq org-agenda-files (my-org-all-todo-files)))

(advice-add 'org-agenda :before #'my-org-agenda-files-update)
(advice-add 'org-todo-list :before #'my-org-agenda-files-update)
#+end_src
***** Visit all files to trigger update
#+begin_src elisp
(defun my-org-agenda-walk-all-roam-files ()
  "Open all org-roam files to trigger an update to their @todo mark"
  (interactive)
  (dolist (file (org-roam-list-files))
    (message "Visiting file %s" file)
    (with-current-buffer (or (find-buffer-visiting file)
			     (find-file-noselect file))
      (my-org-mark-if-has-todo)
      (save-buffer))))
#+end_src
**** Quick-todo
#+begin_src elisp
(defvar quick-todo-filename-trunc-length 20
  "Number of characters to truncate the quick-todo filename string at")

(defun quick-todo ()
  "Prompt infor for a TODO in the minibuffer for a quicker TODO capture"
  (interactive)
  (let*
      ((todo (read-string "Todo: "))
       (deadline (read-string "Deadline [relative]: "))
       (slug (org-roam-node-slug (org-roam-node-create :title todo)))
       (filename
	(format "%d-%s.org"
		(time-convert (current-time) 'integer)
		slug))
       (org-id-overriding-file-name filename)
       id)
    (with-temp-buffer
      (insert ":PROPERTIES:\n:ID:        \n:END:\n#+title: "
	      (substring todo 0 (min quick-todo-filename-trunc-length (length todo)))
	      "\n:END:\n* TODO " todo)
      (goto-char 25)
      (setq id (org-id-get-create))
      (write-file filename)
      (org-roam-db-update-file filename))
    (org-roam-db-sync)))
			 
#+end_src
**** Delete node
#+begin_src elisp
(defun org-roam-delete-current-node ()
  "Delete node currently open w/ confirmation"
  (interactive)
  (if (string-equal (read-string "Delete current node? [yes/no]: ") "yes")
      (progn
	(delete-file (org-roam-node-file (org-roam-node-at-point)))
	(kill-buffer (current-buffer))
	(org-roam-db-sync))
    (message "Didn't get 'yes', not deleting.")))
#+end_src
*** Automatic categorization
**** Tag - category associations
#+begin_src elisp
(setq my-org-categories-alist
      '(("reading" . "READING")))
#+end_src
**** Categorization
#+begin_src elisp
(defun my-org-categorize-by-tags ()
  (interactive)
  (org-map-entries (lambda ()
		     (let*
			 ((tags nil)
			  (targetcat nil))
		       (when-let
			   ;; (tags (split-string (or (org-entry-get (point) "TAGS") "nil") ":" t))
			   (tags (my-org-get-tags-at-point-safe))
			 (dolist (targetcat my-org-categories-alist)
			   (when (member (car targetcat) tags)
			     (org-entry-put (point) "CATEGORY" (cdr targetcat)))))))))
#+end_src
*** Automatic refiling
**** Tag - file associations
#+begin_src elisp
(setq my-org-refile-by-tag-alist
      '(("cse331" . "~/org/roam/cse331.org")))
#+end_src
** Agenda
*** Packages
**** ~org-super-agenda~
#+begin_src elisp
(use-package org-super-agenda
  :hook (org-agenda-mode . org-super-agenda-mode)
  :bind (:map org-super-agenda-header-map
	     ("j" . 'org-agenda-next-line)
	     ("k" . 'org-agenda-previous-line))
  :config
  (setq org-super-agenda-groups
	'((:log t)
	  (:name "Completed today"
		 :regexp "State \"DONE\"")
	  (:name "Schedule"
		 :time-grid t
		 :order 1)
	  (:name "Today"
		 :and (
		       :todo t
		       :deadline today)
		 :order 2)
	  (:name "Overdue"
		 :deadline past
		 :order 97)
	  ;; (:name "Dateless"
	  ;; 	 :date nil
	  ;; 	 :order 99)
	  (:name "AVC"
		 :and (:tag ("@avc" "avc" "AVC") :todo t)
		 :order 12)
	  (:name "WAVES"
		 :tag ("@waves" "WAVES" "waves" "research")
		 :order 11))))
#+end_src
*** Files
#+begin_src elisp
(setq org-agenda-files (my-org-all-todo-files))
#+end_src
*** Prefix format
#+begin_src elisp
(setq org-agenda-prefix-format
      '((agenda . "%+12(my-org-category-prefix) [ %15(string-join (my-org-get-tags-at-point-safe) \":\") ] %5t %s")
	(todo . " %i %-12:c")
	(tags . " %i %-12:c")
	(search . "%i %-12:c")))
#+end_src
*** Appearance
#+begin_src elisp
(set-face-foreground 'org-upcoming-deadline "goldenrod1")
(set-face-foreground 'org-imminent-deadline "tomato1")

(setq org-agenda-deadline-faces
      '((1.0 . org-imminent-deadline)
	(0.5 . org-upcoming-deadline)
	(0.0 . org-upcoming-distant-deadline)))

(setq org-agenda-window-setup "only-window")

(setq org-agenda-span 'day)

;; (setq org-agenda-skip-deadline-if-done nil)
;; (setq org-agenda-skip-function-global '(org-agenda-skip-entry-if 'todo 'done))
(setq org-agenda-skip-deadline-if-done nil)
(setq org-agenda-skip-function-global nil)

#+end_src
*** Deadline warning
#+begin_src elisp
(setq org-deadline-warning-days 7)
#+end_src
*** Category prefix function
#+begin_src elisp
(defun my-org-category-prefix ()
  (interactive)
  (let*
      ((category (org-entry-get (point) "CATEGORY"))
       (fname (file-name-sans-extension (file-name-nondirectory (or (buffer-file-name) "nil")))))
    (if (string-equal category fname)
      "MISC"
      category)))
#+end_src
*** Custom views
#+begin_src elisp
(setq org-agenda-custom-commands
      '(("ces" "Custom: Agenda and Emacs IDEA items"
	 ((org-ql-block '(and (todo) (tags "@avc"))
			((org-ql-block-header "SOMEDAY :Emacs: High-priority")))
	  ))))
#+end_src
** Babel
*** General
#+begin_src elisp
(setq org-src-window-setup 'split-window-below
      org-babel-python-command "python3"
      org-confirm-babel-evaluate nil) ;; Don't ask to execute
#+end_src
*** Packages
**** ~ob-mermaid~
#+begin_src elisp
(use-package ob-mermaid)
#+end_src
*** Languages
#+begin_src elisp
(org-babel-do-load-languages
 'org-babel-load-languages
 '((mermaid . t)
   (python . t)
   (emacs-lisp . t)))
#+end_src
** General appearance
*** Lists
Use a dot in place of a dash for items
#+begin_src elisp
(font-lock-add-keywords 'org-mode
			'(("^ *\\([-]\\) "
			   (0 (prog1 ()
				(compose-region
				 (match-beginning 1)
				 (match-end 1)
				 "â€¢" ))))))
#+end_src
*** Indentation
Indent contextually to outline node level, set indentation width per-indent to 4 spaces, replace ellipsis for folded headings with " ... "
#+begin_src elisp
(setq org-adapt-indentation t
      org-indent-indentation-per-level 4
      org-ellipsis " ... ")
#+end_src
*** Headings
Hide leading stars for headings, use UTF-8 symbols
#+begin_src elisp
(setq org-hide-leading-stars t)
#+end_src
*** Source blocks
#+begin_src elisp
(setq org-src-fontify-natively t
      org-src-tab-acts-natively t
      org-edit-src-content-indentation 0)
#+end_src
*** TODOs
#+begin_src elisp
(setq org-todo-keyword-faces
      '(
	("IDEA" . (:foreground "dark khaki" :weight bold))
	("INACTIVE" . (:foreground "dim gray" :weight bold))
	("TODO" . (:foreground "cyan" :weight bold))
	("DOING" . (:foreground "tan" :weight bold))
	("BLOCKED" . (:foreground "tomato" :weight bold))
	("COMPLETE" . (:foreground "chartreuse" :weight bold))
	("NOT DOING" . (:foreground "dim gray" :weight bold))
	))
#+end_src
*** Packages
**** ~org-bullets~
#+begin_src elisp
(use-package org-bullets
  :hook (org-mode . org-bullets-mode))
;(add-hook 'org-mode-hook (lambda () (org-bullets-mode 1)))
#+end_src
**** ~org-appear~
#+begin_src elisp
(setq org-hide-emphasis-markers t)

(use-package org-appear
  :commands (org-appear-mode)
  :hook (org-mode . org-appear-mode)
  :config
  (setq org-hide-emphasis-markers t)
  (setq org-appear-autoemphasis t
	org-appear-autolinks t
	org-appear-autosubmarkers t))
#+end_src
**** ~org-special-blocks-extra~
#+begin_src elisp
#+end_src
**** ~olivetti~
#+begin_src elisp
(defun my-fix-fucking-olivetti ()
  (interactive)
  (olivetti-set-width 0.75))

(use-package olivetti
  :config
  :hook ((org-mode . olivetti-mode)
	 (olivetti-mode . my-fix-fucking-olivetti)))
#+end_src
*** Faces (re-sort later)
#+begin_src elisp
(with-eval-after-load 'org-faces
  (progn
    (set-face-attribute 'org-block nil :inherit 'fixed-pitch :height 0.85)
    (set-face-attribute 'org-code nil :inherit '(shadow fixed-pitch))
    (set-face-attribute 'org-verbatim nil :inherit '(shadow fixed-pitch))
    (set-face-attribute 'org-special-keyword nil :inherit '(font-lock-comment-face fixed-pitch))
    (set-face-attribute 'org-meta-line nil :inherit '(font-lock-comment-face fixed-pitch))
    (set-face-attribute 'org-checkbox nil :inherit 'fixed-pitch)
    (set-face-attribute 'org-drawer nil :inherit '(shadow fixed-pitch))
    (set-face-attribute 'org-table nil :inherit 'fixed-pitch)
    (set-face-attribute 'org-hide nil :inherit 'fixed-pitch))
    ;; (set-face-attribute 'org-block-begin-line nil :inherit '(org-hide))
    ;; (set-face-attribute 'org-block-begin-line nil :inherit '(org-hide default))
    ;; (set-face-attribute 'org-block nil :background "#282828"))
    ;; (set-face-attribute 'org-block-end-line nil :extend t))
    (require 'org-indent)
    (set-face-attribute 'org-indent nil :inherit '(org-hide fixed-pitch) :height 0.85))

(add-hook 'org-mode-hook 'variable-pitch-mode)
#+end_src
** Export
*** Packages
**** ~ox-md~
#+begin_src elisp
(require 'ox-md)
#+end_src
** LaTeX
*** Packages
**** ~org-fragtog~
#+begin_src elisp
(use-package org-fragtog
  :hook (org-mode . org-fragtog-mode))
#+end_src
*** Fragment formatting
#+begin_src elisp
(setq org-format-latex-options
      '(:foreground default
		    :background default
		    :scale 2.0
		    :html-foreground "Transparent"
		    :html-background "Transparent"
		    :justify 'right
		    :html-scale 1.0
		    :matchers ("begin" "$1" "$" "$$" "\\(" "\\[" )))
#+end_src
*** LaTeX Packages
#+begin_src elisp
(with-eval-after-load 'org 
     (add-to-list 'org-latex-packages-alist '("" "amsfonts" t))
     (add-to-list 'org-latex-packages-alist '("" "amsmath" t))
     (add-to-list 'org-latex-packages-alist '("" "amsthm" t))
     (add-to-list 'org-latex-packages-alist '("" "amssymb" t))
     (setq org-format-latex-options 
           (plist-put org-format-latex-options :scale 1.4)))
#+end_src
*** Centering
#+begin_src elisp
;; specify the justification you want
;; (plist-put org-format-latex-options :justify 'center)

(defun org-justify-fragment-overlay (beg end image imagetype)
  "Adjust the justification of a LaTeX fragment.
The justification is set by :justify in
`org-format-latex-options'. Only equations at the beginning of a
line are justified."
  (cond
   ;; Centered justification
   ((and (eq 'center (plist-get org-format-latex-options :justify)) 
         (= beg (line-beginning-position)))
    (let* ((img (create-image image 'imagemagick t))
           (width (car (image-size img)))
           (offset (floor (- (/ (window-text-width) 2) (/ width 2)))))
      (overlay-put (ov-at) 'before-string (make-string offset ? ))))
   ;; Right justification
   ((and (eq 'right (plist-get org-format-latex-options :justify)) 
         (= beg (line-beginning-position)))
    (let* ((img (create-image image 'imagemagick t))
           (width (car (image-display-size (overlay-get (ov-at) 'display))))
           (offset (floor (- (window-text-width) width (- (line-end-position) end)))))
      (overlay-put (ov-at) 'before-string (make-string offset ? ))))))

(defun org-latex-fragment-tooltip (beg end image imagetype)
  "Add the fragment tooltip to the overlay and set click function to toggle it."
  (overlay-put (ov-at) 'help-echo
               (concat (buffer-substring beg end)
                       "mouse-1 to toggle."))
  (overlay-put (ov-at) 'local-map (let ((map (make-sparse-keymap)))
                                    (define-key map [mouse-1]
                                      `(lambda ()
                                         (interactive)
                                         (org-remove-latex-fragment-image-overlays ,beg ,end)))
                                    map)))

(advice-add 'org--format-latex-make-overlay :after 'org-justify-fragment-overlay)
(advice-add 'org--format-latex-make-overlay :after 'org-latex-fragment-tooltip)
#+end_src
*** General
#+begin_src elisp
(setq org-startup-with-inline-images t
      org-startup-with-latex-preview t
      org-pretty-entities nil
      org-pretty-entities-include-sub-superscripts nil
      org-highlight-latex-and-related '(native))
#+end_src

** Message completion
#+begin_src elisp
(message "Config-status: Completed Org")
#+end_src
